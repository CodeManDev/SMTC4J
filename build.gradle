plugins {
    id 'java'
}

group = 'dev.codeman'
version = '0.0.6'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.13.2'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

def nativeDownloadDir = layout.buildDirectory.dir("native-download").get().asFile
def downloadedNativeDllFile = nativeDownloadDir.toPath().resolve("SMTC4J.dll").toFile()
def resourcesNativeDir = layout.buildDirectory.dir("resources/main/native").get().asFile
def nativeDllFile = file("src/main/resources/native/SMTC4J.dll") // for dev environment

tasks.register("downloadNative") {
    doLast {
        if (downloadedNativeDllFile.exists() || nativeDllFile.exists()) {
            println "Native DLL already exists, skipping download."
            return
        }

        nativeDownloadDir.mkdirs()
        def url = "https://github.com/CodeManDev/SMTC4J/releases/download/${version}/SMTC4J.dll"
        println "Downloading native DLL from $url"

        new URL(url).withInputStream { input ->
            downloadedNativeDllFile.withOutputStream { output ->
                output << input
            }
        }
    }
}

tasks.register("copyNativeIntoResources", Copy) {
    dependsOn "downloadNative"
    from(downloadedNativeDllFile)
    into(resourcesNativeDir)
    onlyIf { downloadedNativeDllFile.exists() }
}

tasks.named("processResources") {
    dependsOn "copyNativeIntoResources"
}
