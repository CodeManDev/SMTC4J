plugins {
    id 'java'
}

group = 'dev.codeman'
version = '0.0.5'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.13.2'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

def nativeDownloadDir = layout.buildDirectory.dir("native-download").get().asFile
def nativeDllFile = nativeDownloadDir.toPath().resolve("SMTC4J.dll").toFile()
def resourcesNativeDir = layout.buildDirectory.dir("resources/main/native").get().asFile

tasks.register("downloadNative") {
    doLast {
        if (nativeDllFile.exists()) {
            println "Native DLL already exists, skipping download."
            return
        }

        nativeDownloadDir.mkdirs()
        def tag = "v$version"
        def url = "https://github.com/CodeManDev/SMTC4J/releases/download/${tag}/SMTC4J.dll"
        println "Downloading native DLL from $url"

        new URL(url).withInputStream { input ->
            nativeDllFile.withOutputStream { output ->
                output << input
            }
        }
    }
}

tasks.register("copyNativeIntoResources", Copy) {
    dependsOn "downloadNative"
    from(nativeDllFile)
    into(resourcesNativeDir)
}

tasks.named("processResources") {
    dependsOn "copyNativeIntoResources"
}
